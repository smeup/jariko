/*
 * Copyright 2019 Sme.UP S.p.A.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

//-----------------
//Main build script
//-----------------
buildscript {

    repositories {
        mavenCentral()
        maven {
            name 'JFrog OSS snapshot repo'
            url  'https://oss.jfrog.org/oss-snapshot-local/'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:12.3.0"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlinVersion"
    }
}

plugins {
    id("io.github.gradle-nexus.publish-plugin") version "2.0.0"
    id "org.ajoberstar.grgit" version "5.3.2"
}

project.group = jarikoGroupId
project.version = jarikoVersion

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.jlleitschuh.gradle.ktlint'
    apply plugin: 'kotlinx-serialization'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name 'Smeup Nexus'
            url 'https://repo.smeup.cloud/nexus/repository/public/'
        }
        maven { url 'https://jitpack.io' }
        // the sonatype repo inclusion allows to work with the reload snapshot version
        maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://oss.jfrog.org/oss-snapshot-local/' }
        jcenter()
    }

    // project.group and project.version are used to identify jariko maven coordinates
    // If you have a project depending on jariko and you want to test
    // some jariko feature without waiting for jitpack.io, you can type:
    // ./gradlew install
    // This task will install jariko artifacts into your local maven repository
    project.group = jarikoGroupId
    project.version = jarikoVersion

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from(components.java)
            }
        }
    }
}

nexusPublishing {
    // trick to bypass staging when we publishToSmeup, because smeup nexus does not support the staging features
    // however take in account that this flag in the other cases must be true or false depending on the version name
    useStaging = !(project.gradle.startParameter.taskNames.contains("publishToSmeup") || jarikoVersion.endsWith("SNAPSHOT"))
    repositories {
        sonatype {
            nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
            snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            username = findProperty("sonatypeUsername")
            password = findProperty("sonatypePassword")
        }
        smeup {
            nexusUrl = uri("https://repo.smeup.cloud/nexus/content/repositories/releases/")
            //when useStaging=false it is always used snapshotRepositoryUrl
            snapshotRepositoryUrl = jarikoVersion.endsWith("SNAPSHOT") ?
                    uri("https://repo.smeup.cloud/nexus/content/repositories/snapshots/"):
                    uri("https://repo.smeup.cloud/nexus/content/repositories/releases/")
            username = findProperty("smeupUsername")
            password = findProperty("smeupPassword")
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'A JAva virtual machine Rpg Interpreter written in KOtlin'
            name 'Jariko'
            url 'https://github.com/smeup/jariko'
            organization {
                name 'com.github.smeup'
                url 'https://github.com/smeup'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/smeup/jariko/issues'
            }
            licenses {
                license {
                    name 'Apache License 2.0'
                    url 'https://github.com/smeup/jariko/blob/master/LICENSE.md'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/smeup/jariko'
                connection 'scm:git:git://github.com/smeup/jariko.git'
                developerConnection 'scm:git:ssh://git@github.com:smeup/jariko.git'
            }
            developers {
                developer {
                    name 'smeup'
                }
            }
        }
    }
}



