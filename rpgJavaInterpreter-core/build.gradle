/*
 * Copyright 2019 Sme.UP S.p.A.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import java.text.SimpleDateFormat

//-----------------
//interpreter-core build script
//-----------------
buildscript {
    ext.kotlinVersion = "${kotlinVersion}"
    ext.jvmVersion = "${jvmVersion}"
    ext.antlrVersion = "${antlrVersion}"
    ext.junitVersion = "${junitVersion}"

    repositories {
        mavenCentral()
        maven {
            name 'JFrog OSS snapshot repo'
            url  'https://oss.jfrog.org/oss-snapshot-localrun/'
        }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

apply plugin: 'antlr'

def antlrVersion = ext.antlrVersion
def junitVersion = ext.junitVersion
def generatedMain = "generated-src/antlr/main"
def generatedMainFile = file(generatedMain)

def unlimitedStringTypeFlag = "jariko.features.UnlimitedStringTypeFlag"

dependencies {
    antlr "org.antlr:antlr4:$antlrVersion"
    implementation "org.antlr:antlr4-runtime:$antlrVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    api project(":kolasu")
    api project(":dspfparser")

    implementation "org.apache.logging.log4j:log4j-api-kotlin:1.5.0"
    implementation "org.apache.logging.log4j:log4j-api:2.25.1"
    implementation 'org.apache.logging.log4j:log4j-core:2.25.1'

    implementation 'commons-io:commons-io:2.20.0'
    implementation("com.github.ajalt:clikt:3.5.2") {
        exclude group: 'com.github.ajalt.clikt', module: 'clikt-linuxx64'
        exclude group: 'com.github.ajalt.clikt', module: 'clikt-mingwx64'
    }

    api "io.github.smeup.reload:base:$reloadVersion"
    api "io.github.smeup.reload:sql:$reloadVersion"
    api "io.github.smeup.reload:nosql:$reloadVersion"
    api "io.github.smeup.reload:manager:$reloadVersion"
    api "io.github.smeup.reload:jt400:$reloadVersion"

    implementation 'com.github.ziggy42:kolor:1.0.0'

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$serializationVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-cbor:$serializationVersion"

    implementation 'org.reflections:reflections:0.10.2'

    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation 'org.hsqldb:hsqldb:2.7.4'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:6.0.0'
}

configurations.all() {
    resolutionStrategy.force "org.antlr:antlr4-runtime:$antlrVersion"
    resolutionStrategy.force "org.antlr:antlr4-tool:$antlrVersion"
}

generateGrammarSource {
    inputs.files fileTree('src/main/antlr').include('**/*.g4')
    maxHeapSize = "64m"
    arguments += ['-package', 'com.smeup.rpgparser']
    outputDirectory = file(generatedMain + "/com/smeup/rpgparser")
}

compileJava {
    sourceCompatibility = "$jvmVersion"
    targetCompatibility = "$jvmVersion"
    dependsOn generateGrammarSource
    source generatedMainFile, sourceSets.main.java
}

compileTestKotlin {
    sourceCompatibility = "$jvmVersion"
    targetCompatibility = "$jvmVersion"
    dependsOn generateTestGrammarSource
}

compileKotlin {
    sourceCompatibility = "$jvmVersion"
    targetCompatibility = "$jvmVersion"
    source generatedMainFile, sourceSets.main.java, sourceSets.main.kotlin
    dependsOn generateGrammarSource
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    compilerOptions {
        jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.fromTarget("$jvmVersion"))
    }
}

ktlintTestSourceSetCheck {
    dependsOn generateTestGrammarSource
}

clean {
    delete file(generatedMain)
    mkdir generatedMain
}

idea {
    module {
        println 'Adding ' + generatedMainFile + ' to source dirs'
        mkdir generatedMain
        sourceDirs += generatedMainFile
        println sourceDirs
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        showCauses true
        showExceptions true
        showStackTraces true
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "------------------------------------------------------------------------------------"
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                println "------------------------------------------------------------------------------------"
            }
        }
    }
}

//Always reruns all non-performance tests displaying just failing items
task testCore(type: Test) {
    outputs.upToDateWhen {false}
    testLogging {
        events "failed"
    }
    useJUnit {
        excludeCategories 'com.smeup.rpgparser.PerformanceTest'
    }
}

test {
    systemProperty unlimitedStringTypeFlag, System.getProperty(unlimitedStringTypeFlag)
    testLogging {
        events "failed"
    }
    useJUnit {
        excludeCategories 'com.smeup.rpgparser.PerformanceTest'
    }
}

//If you want to collect data about failed performance tests, run this task with:
//gradlew testPerformance -DexportCsvFile="/some/file.csv"
task testPerformance(type: Test) {
    systemProperty unlimitedStringTypeFlag, System.getProperty(unlimitedStringTypeFlag)
    systemProperty 'exportCsvFile', System.getProperty('exportCsvFile')
    maxHeapSize = "2048m"
    testLogging {
        events "passed", "skipped", "failed"
    }
    useJUnit {
        includeCategories 'com.smeup.rpgparser.PerformanceTest'
    }
}

//gradlew testDBPerformance -DjrkCsvOutputDir="/home/.../myCsvResults" -DjrkConfigurationFile="/home/.../jrkDBConfigurations/as400-db-configurations.properties"
task testDBPerformance(type: Test) {
    systemProperty 'jrkCsvOutputDir', System.getProperty('jrkCsvOutputDir', System.getProperty("java.io.tmpdir"))
    systemProperty 'jrkEXEC_MUTErepeat', System.getProperty('jrkEXEC_MUTErepeat', '1')
    maxHeapSize = "2048m"
    testLogging {
        events "passed", "skipped", "failed"
    }
    useJUnit {
        includeCategories 'com.smeup.rpgparser.DBPerformanceTest'
    }
}

task testAll(type: Test) {
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams true
    }
}

//gradlew runExample -Pargs="CALCFIB,9"
task runExample (type: JavaExec) {
    if (project.hasProperty('programArgs')){
        args(project.getProperties().get("programArgs").split(','))
    }
    main="com.smeup.rpgparser.execution.RunnerKt"
    classpath = sourceSets.test.runtimeClasspath
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.smeup.rpgparser.execution.RunnerKt'
    }
    archiveBaseName = project.name + '-all'
    archiveVersion = ''
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

//Creates smeup-rpg/rpgJavaInterpreter-core/build/libs/rpgJavaInterpreter-core-mute-all.jar
task fatMuteJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.smeup.rpgparser.mute.StandaloneMuteRunner'
    }
    archiveBaseName = project.name + '-mute-all'
    archiveVersion = ''
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

//gradlew runMutes -PmuteDir="/some/dir"
task runMutes(type: JavaExec) {
    dependsOn compileKotlin

    def muteDir = '../mutes_for_ci'
    if (project.hasProperty("muteDir")){
        muteDir = project.getProperties().get("muteDir")
        println 'Mute directory is ' + muteDir
    }
    classpath = sourceSets.main.runtimeClasspath
    main = 'com.smeup.rpgparser.mute.MuterunnerKt'
    args muteDir, "-v", "-lc", "../mutes_for_ci/mute_logging.config"
}

task profileRpgProgram(type: JavaExec) {
    String usage = """Usage:
>./gradlew $name -PrpgProgram=rpg_program_path [-PcompiledProgramDir=compiled_program_dir] [-PlogConfigurationFile=log_conf_file]
- rpg_program_path is RPG program relative or absoulte path.
Relative path is \"relative\" to  ${rootDir.path} directory.
Optional params
- compiled_program_dir Optimization option. 
  If passed, jariko will look in this directory programname.bin, if it finds it, that program will be executed
- log_conf_file Debug option. 
  Logging configuration file, view: https://github.com/smeup/jariko/blob/master/docs/logging.md"""
    classpath = sourceSets.main.runtimeClasspath
    main = "com.smeup.rpgparser.execution.RunnerKt"
    description = """profileRpgProgram allows to understand how Jariko interprets any RPG Program.
It creates a jfr file (java flight recorder) containing profiling informations. 
After task execution, you can analize jfr file with JMC utility (Oracle Java Mission Control).

$usage
"""
    ignoreExitValue = true
    doFirst {
        if (project.hasProperty("rpgProgram")) {
            String timestamp = new SimpleDateFormat("yyyyMMddHHmmss").format(new Date())
            File rpgProgramFile = new File(project.rpgProgram)
            File jfrPath =  new File(buildDir.path, "jfr/$timestamp-${rpgProgramFile.name}.jfr")
            if (!jfrPath.getParentFile().exists()) {
                jfrPath.getParentFile().mkdirs()
            }
            boolean absolute = project.rpgProgram.matches("^\\w:.+|^/.+")
            File compiledProgramDir = project.hasProperty("compiledProgramDir") ? new File(project.compiledProgramDir) : null
            File logConfigurationFile = project.hasProperty("logConfigurationFile") ? new File(project.logConfigurationFile) : null
            List<String> arguments = []
            if (compiledProgramDir) {
                arguments = arguments + "-cpd" + compiledProgramDir.getPath()
            }
            if (logConfigurationFile) {
                arguments = arguments + "-lc" + logConfigurationFile.getPath()
            }
            if (absolute) {
                arguments =  arguments + project.rpgProgram
            }
            else {
                arguments =  arguments + "${rootDir.path}/$project.rpgProgram"
            }
            args = (arguments)
            jvmArgs "-XX:+FlightRecorder",
                    "-XX:StartFlightRecording=duration=100s,filename=${jfrPath.path},settings=${rootDir.path}/profiling.jfc"
        }
        else {
            String messageError = """Missing parameter rpgProgram.
${usage} 
Type: ./gradlew help --task $name
for further informations."""
            throw new IllegalArgumentException(messageError)
        }
    }
}

task profileMainClass (type: JavaExec) {
    String usage = """Usage:
>./gradlew $name -PmainClass=main_class
Remember that for kotlin main_class (.kt source file extension) you need to append the suffix Kt in the class name. 
For example if your kotlin class is  foo.HelloWorld, the command syntax will be:
>./gradlew $name -PmainClass=foo.HelloWorldKt
"""
    classpath = sourceSets.test.runtimeClasspath
    description = """$name allows to profile a main class.
It creates a jfr file (java flight recorder) containing profiling informations. 
After task execution, you can analize jfr file with JMC utility (Oracle Java Mission Control).

$usage
"""
    ignoreExitValue = true
    doFirst {
        if (project.hasProperty("mainClass")) {
            main = project.mainClass
            String timestamp = new SimpleDateFormat("yyyyMMddHHmmss").format(new Date())
            File rpgProgramFile = new File(project.mainClass.substring(project.mainClass.lastIndexOf(".") + 1))
            File jfrPath =  new File(buildDir.path, "jfr/$timestamp-${rpgProgramFile.name}.jfr")
            if (!jfrPath.getParentFile().exists()) {
                jfrPath.getParentFile().mkdirs()
            }
            jvmArgs "-XX:+UnlockCommercialFeatures",
                    "-XX:+FlightRecorder",
                    "-XX:StartFlightRecording=duration=100s,filename=${jfrPath.path}",
                    "-XX:FlightRecorderOptions=${project.FlightRecorderOptions}",
                    "-XX:+UnlockDiagnosticVMOptions",
                    "-XX:+DebugNonSafepoints"
        }
        else {
            String messageError = """Missing parameter mainClass.
${usage} 
Type: ./gradlew help --task $name
for further informations."""
            throw new IllegalArgumentException(messageError)
        }
    }
}

task compileAllMutes(type: JavaExec) {
    main="com.smeup.rpgparser.TestingUtils"
    classpath = sourceSets.test.runtimeClasspath
}

task compilePerformanceMutes(type: JavaExec) {
    systemProperty unlimitedStringTypeFlag, System.getProperty(unlimitedStringTypeFlag)
    enabled = System.getProperty('jariko.compilePerformanceMutes', 'true') == 'true'
    main="com.smeup.rpgparser.TestingUtils"
    classpath = sourceSets.test.runtimeClasspath
    args "-dirs", "performance,performance-ast"
}

tasks.withType(Test) {
    // Default system properties passed to jariko for Test type tasks
    systemProperty 'featuresFactory', System.getProperty('featuresFactory', 'default')
    systemProperty 'jrkReloadConfig', System.getProperty('jrkReloadConfig')
}

tasks.withType(JavaExec) {
    // Default system properties passed to jariko for JavaExec type tasks
    systemProperty 'featuresFactory', System.getProperty('featuresFactory', 'default')
    systemProperty 'jrkReloadConfig', System.getProperty('jrkReloadConfig')
}

task javadocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

task createVersion() {
    new File(projectDir, "src/main/resources/META-INF/com.smeup.jariko/version.txt").text = """
Version: $project.version
Branch: ${grgit.branch.current.name}
Revision: ${grgit.head().abbreviatedId}
Buildtime: ${new SimpleDateFormat("dd-MM-yyyy HH:mm:ss").format(new Date())}
""".trim()
}

check.dependsOn compileAllMutes,runMutes
testPerformance.dependsOn compilePerformanceMutes

// deploy
publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId jarikoGroupId
            artifactId 'rpgJavaInterpreter-core'
            version jarikoVersion
        }
    }
}

signing {
    if (!project.hasProperty("signing.keyId")) {
        println "$project.name - Signing disabled because signing.keyId property is not present, it is not an error!!!"
    }
    required {  project.hasProperty("signing.keyId") }
    sign publishing.publications.mavenJava
}

java {
    withJavadocJar()
    withSourcesJar()
}
